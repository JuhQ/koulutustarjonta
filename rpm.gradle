apply plugin: 'rpm'

task rpm(type: Rpm) {
    def rootFolder = "/opt/$project.name"
    def env = project.hasProperty('env') ? project.getProperty('env') : "";

    packageName = project.name
    arch = NOARCH
    os = LINUX
    summary = project.name
    description = "University of Helsinkig : Koulutustarjonta : $project.name"
    user = "koultarj"

    preInstall = file('src/deploy/rpm/pre_install')
    postInstall = file('src/deploy/rpm/post_install')
    preUninstall = file('src/deploy/rpm/pre_uninstall')

    into rootFolder

    //application executable
    from("build/libs") {
        include project.name + "-" + project.version + ".jar"
        //remove version from file to avoid having to redo config files on every release
        rename { String fileName ->
            fileName.replace("-$project.version", '')
        }
    }

    //configuration file
    from('config/' + env) {
        fileType = CONFIG
        into rootFolder + '/config/'
        // insert db configuration parameters
        expand(db_url: project.hasProperty('db.url') ? project.getProperty('db.url') : "",
                db_user: project.hasProperty('db.user') ? project.getProperty('db.user') : "",
                db_passwd: project.hasProperty('db.passwd') ? project.getProperty('db.passwd') : "",
                api_endpoint: project.hasProperty('api.endpoint') ? project.getProperty('api.endpoint') : "")
    }

    // Upstart file for RHEL
    from('src/deploy/upstart') {
        fileType = CONFIG
        into '/'
    }
}
