subprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    sourceCompatibility = '1.8'
    group = 'fi.helsinki.koulutustarjonta'
    version = '1.0-SNAPSHOT'

    repositories {
        mavenCentral()
    }

    dependencies {
        compile "org.projectlombok:lombok:1.14.4"
    }

    test {
        testLogging {
            afterSuite { desc, result ->
                if (!desc.parent) { // will match the outermost suite
                    println "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
                }
            }
        }
    }

}

def commitHash() {
    def env = System.getenv()
    def hash = env['GIT_COMMIT']
    println("Jenkins git commit hash environment variable: " + hash)
    if (hash == null) {
        println("git commit jenkins variable not found, resolving commit from git HEAD")
        def cmd = "git rev-parse --short HEAD"
        try {
            def proc = cmd.execute()
            hash = proc.text.trim()
        }
        catch (Exception e) {
            println("Could not execute git command, couldn't resolve hash")
            hash = null;
        }
    } else {
        hash = hash[0..6]
    }
    hash
}

